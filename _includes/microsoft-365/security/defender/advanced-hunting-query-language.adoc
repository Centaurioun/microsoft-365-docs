= 
schmurky
:keywords: advanced hunting, threat hunting, cyber threat hunting,
Microsoft 365 Defender, microsoft 365, m365, search, query, language,
learn, first query, telemetry, events, telemetry, custom detections,
schema, kusto, operators, data types, powershell download, query example

== Learn the advanced hunting query language

{empty}[!INCLUDE link:../includes/microsoft-defender.md[Microsoft 365
Defender rebranding]]

*Applies to:*

* Microsoft 365 Defender

Advanced hunting is based on the link:/azure/kusto/query/[Kusto query
language]. You can use Kusto operators and statements to construct
queries that locate information in a specialized
link:advanced-hunting-schema-tables.md[schema].

Watch this short video to learn some handy Kusto query language basics.

____
{empty}[!VIDEO https://www.microsoft.com/en-us/videoplayer/embed/RWRwfJ]
____

To understand these concepts better, run your first query.

=== Try your first query

In the Microsoft 365 Defender portal, go to *Hunting* to run your first
query. Use the following example:

[source,kusto]
----
// Finds PowerShell execution events that could involve a download
union DeviceProcessEvents, DeviceNetworkEvents
| where Timestamp > ago(7d)
// Pivoting on PowerShell processes
| where FileName in~ ("powershell.exe", "powershell_ise.exe")
// Suspicious commands
| where ProcessCommandLine has_any("WebClient",
 "DownloadFile",
 "DownloadData",
 "DownloadString",
"WebRequest",
"Shellcode",
"http",
"https")
| project Timestamp, DeviceName, InitiatingProcessFileName, InitiatingProcessCommandLine, 
FileName, ProcessCommandLine, RemoteIP, RemoteUrl, RemotePort, RemoteIPType
| top 100 by Timestamp
----

*https://security.microsoft.com/hunting?query=H4sIAAAAAAAEAI2TW0sCURSF93PQfxh8Moisp956yYIgQtLoMaYczJpbzkkTpN_et_dcdPQkcpjbmrXXWftyetKTQG5lKqmMpeB9IJksJJKZDOWdZ8wKeP5wvcm3OLgZbMXmXCmIxjnYIfcAVgYvRi8w3TnfsXEDGAG47pCCZXyP5ViO4KeNbt-Up-hEuJmB6lvButnY8XSL-cDl0M2I-GwxVX8Fe2H5zMzHiKjEVB0eEsnBrszfBIWuXOLrxCJ7VqEBfM3DWUYTkNKrv1p5y3X0jwetemzOQ_NSVuuXZ1c6aNTKRaN8VvWhY9n7OS-o6J5r7mYeQypdEKc1m1qfiqpjCSuspsDntt2J61bEvTlXls5AgQfFl5bHM_gr_BhO2RF1rztoBv2tWahrso_TtzkL93KGMGZVr2pe7eWR-xeZl91f_113UOsx3nDR4Y9j5R6kaCq8ajr_YWfFeedsd27L7it-Z6dAZyxsJq1d9-2ZOSzK3y2NVd8-zUPjtZaJnYsIH4Md7AmdeAcd2Cl1XoURc5PzXlfU8U9P54WcswL6t_TW9Q__qX-xygQAAA&runQuery=true&timeRangeId=week[Run
this query in advanced hunting]*

==== Describe the query and specify the tables to search

A short comment has been added to the beginning of the query to describe
what it is for. This comment helps if you later decide to save the query
and share it with others in your organization.

[source,kusto]
----
// Finds PowerShell execution events that could involve a download
----

The query itself will typically start with a table name followed by
several elements that start with a pipe (`|`). In this example, we start
by creating a union of two tables, `DeviceProcessEvents` and
`DeviceNetworkEvents`, and add piped elements as needed.

[source,kusto]
----
union DeviceProcessEvents, DeviceNetworkEvents
----

==== Set the time range

The first piped element is a time filter scoped to the previous seven
days. Limiting the time range helps ensure that queries perform well,
return manageable results, and don’t time out.

[source,kusto]
----
| where Timestamp > ago(7d)
----

==== Check specific processes

The time range is immediately followed by a search for process file
names representing the PowerShell application.

[source,kusto]
----
// Pivoting on PowerShell processes
| where FileName in~ ("powershell.exe", "powershell_ise.exe")
----

==== Search for specific command strings

Afterwards, the query looks for strings in command lines that are
typically used to download files using PowerShell.

[source,kusto]
----
// Suspicious commands
| where ProcessCommandLine has_any("WebClient",
    "DownloadFile",
    "DownloadData",
    "DownloadString",
    "WebRequest",
    "Shellcode",
    "http",
    "https")
----

==== Customize result columns and length

Now that your query clearly identifies the data you want to locate, you
can define what the results look like. `project` returns specific
columns, and `top` limits the number of results. These operators help
ensure the results are well-formatted and reasonably large and easy to
process.

[source,kusto]
----
| project Timestamp, DeviceName, InitiatingProcessFileName, InitiatingProcessCommandLine, 
FileName, ProcessCommandLine, RemoteIP, RemoteUrl, RemotePort, RemoteIPType
| top 100 by Timestamp
----

Select *Run query* to see the results.

____
{empty}[!TIP] You can view query results as charts and quickly adjust
filters. For guidance, link:advanced-hunting-query-results.md[read about
working with query results]
____

=== Learn common query operators

You’ve just run your first query and have a general idea of its
components. It’s time to backtrack slightly and learn some basics. The
Kusto query language used by advanced hunting supports a range of
operators, including the following common ones.

[width="100%",cols="50%,50%",options="header",]
|===
|Operator |Description and usage
|`where` |Filter a table to the subset of rows that satisfy a predicate.

|`summarize` |Produce a table that aggregates the content of the input
table.

|`join` |Merge the rows of two tables to form a new table by matching
values of the specified column(s) from each table. Watch
https://www.youtube.com/watch?v=8qZx7Pp5XgM[Joining tables in KQL] to
learn how.

|`count` |Return the number of records in the input record set.

|`top` |Return the first N records sorted by the specified columns.

|`limit` |Return up to the specified number of rows.

|`project` |Select the columns to include, rename or drop, and insert
new computed columns.

|`extend` |Create calculated columns and append them to the result set.

|`makeset` |Return a dynamic (JSON) array of the set of distinct values
that Expr takes in the group.

|`find` |Find rows that match a predicate across a set of tables.
|===

To see a live example of these operators, run them from the *Get
started* section in advanced hunting.

=== Understand data types

Advanced hunting supports Kusto data types, including the following
common types:

[width="100%",cols="50%,50%",options="header",]
|===
|Data type |Description and query implications
|`datetime` |Data and time information typically representing event
timestamps.
link:/azure/data-explorer/kusto/query/scalar-data-types/datetime[See
supported datetime formats]

|`string` |Character string in UTF-8 enclosed in single quotes (`'`) or
double quotes (`"`).
link:/azure/data-explorer/kusto/query/scalar-data-types/string[Read more
about strings]

|`bool` |This data type supports `true` or `false` states.
link:/azure/data-explorer/kusto/query/scalar-data-types/bool[See
supported literals and operators]

|`int` |32-bit integer

|`long` |64-bit integer
|===

To learn more about these data types,
link:/azure/data-explorer/kusto/query/scalar-data-types/[read about
Kusto scalar data types].

=== Get help as you write queries

Take advantage of the following functionality to write queries faster: -
*Autosuggest*—as you write queries, advanced hunting provides
suggestions from IntelliSense. - *Schema tree*—a schema representation
that includes the list of tables and their columns is provided next to
your working area. For more information, hover over an item.
Double-click an item to insert it to the query editor. -
*link:advanced-hunting-schema-tables.md#get-schema-information-in-the-security-center[Schema
reference]*—in-portal reference with table and column descriptions as
well as supported event types (`ActionType` values) and sample queries

=== Work with multiple queries in the editor

You can use the query editor to experiment with multiple queries. To use
multiple queries:

* Separate each query with an empty line.
* Place the cursor on any part of a query to select that query before
running it. This will run only the selected query. To run another query,
move the cursor accordingly and select *Run query*.

:::image type=``content'' source=``../../media/multiple-queries.png''
alt-text=``An example of multiple queries execution in the *New query*
page in the Microsoft 365 Defender portal''
lightbox=``../../media/multiple-queries.png'':::

For a more efficient workspace, you can also use multiple tabs in the
same hunting page. Select *New query* to open a tab for your new query.

:::image type=``content'' source=``../../media/multitab.png''
alt-text=``Opening a new tab by selecting Create new in advanced hunting
in the Microsoft 365 Defender portal''
lightbox=``../../media/multitab.png'':::

You can then run different queries without ever opening a new browser
tab.

:::image type=``content'' source=``../../media/multitab-examples.png''
alt-text=``Run different queries without ever leaving the advanced
hunting page in the Microsoft 365 Defender portal''
lightbox=``../../media/multitab-examples.png'':::

____
[!NOTE] Using multiple browser tabs with advanced hunting might cause
you to lose your unsaved queries. To prevent this from happening, use
the tab feature within advanced hunting instead of separate browser
tabs.
____

=== Use sample queries

The *Get started* section provides a few simple queries using commonly
used operators. Try running these queries and making small modifications
to them.

:::image type=``content'' source=``../../media/get-started-section.png''
alt-text=``The *Getting started* section in the *Advanced hunting* page
in the Microsoft 365 Defender portal''
lightbox=``../../media/get-started-section.png'':::

____
[!NOTE] Apart from the basic query samples, you can also access
link:advanced-hunting-shared-queries.md[shared queries] for specific
threat hunting scenarios. Explore the shared queries on the left side of
the page or the https://aka.ms/hunting-queries[GitHub query repository].
____

=== Access query language documentation

For more information on Kusto query language and supported operators,
see link:/azure/kusto/query/[Kusto query language documentation].

____
[!NOTE] Some tables in this article might not be available in Microsoft
Defender for Endpoint. link:m365d-enable.md[Turn on Microsoft 365
Defender] to hunt for threats using more data sources. You can move your
advanced hunting workflows from Microsoft Defender for Endpoint to
Microsoft 365 Defender by following the steps in
link:advanced-hunting-migrate-from-mde.md[Migrate advanced hunting
queries from Microsoft Defender for Endpoint].
____

=== Related topics

* link:advanced-hunting-overview.md[Advanced hunting overview]
* link:advanced-hunting-query-results.md[Work with query results]
* link:advanced-hunting-shared-queries.md[Use shared queries]
* link:advanced-hunting-query-emails-devices.md[Hunt across devices&#44;
emails&#44; apps&#44; and identities]
* link:advanced-hunting-schema-tables.md[Understand the schema]
* link:advanced-hunting-best-practices.md[Apply query best practices]
